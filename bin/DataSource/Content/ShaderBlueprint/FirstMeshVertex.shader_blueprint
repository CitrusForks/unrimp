/*********************************************************\
 * Copyright (c) 2012-2015 Christian Ofenberg
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy of this software
 * and associated documentation files (the "Software"), to deal in the Software without
 * restriction, including without limitation the rights to use, copy, modify, merge, publish,
 * distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the
 * Software is furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all copies or
 * substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING
 * BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
 * NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM,
 * DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
 * FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
\*********************************************************/


//[-------------------------------------------------------]
//[ Includes                                              ]
//[-------------------------------------------------------]
@includepiece(15)	// "Core.shader_piece"
@includepiece(16)	// "FirstMesh.shader_piece"


//[-------------------------------------------------------]
//[ Input / output                                        ]
//[-------------------------------------------------------]
// Attribute input / output
struct VS_INPUT
{
	float3 Position : POSITION;		// Object space vertex position
	int2   TexCoord : TEXCOORD0;	// 16 bit texture coordinate
	int4   QTangent : NORMAL;		// 16 bit QTangent
};
struct VS_OUTPUT
{
	float4   Position     : SV_POSITION;	// Clip space vertex position as output, left/bottom is (-1,-1) and right/top is (1,1)
	float2   TexCoord     : TEXCOORD0;		// Texture coordinate
	float3x3 TangentFrame : TEXCOORD1;		// Tangent frame
};

// Uniform buffers
cbuffer PassUniformBuffer : register(b0)
{
	float4x4 WorldSpaceToViewSpaceMatrix;
	float4x4 WorldSpaceToClipSpaceMatrix;
	float	 StaticPropertyTest;
}
cbuffer InstanceUniformBuffer : register(b2)
{
	float4x4 ObjectSpaceToWorldSpaceMatrix;
	int		 MaterialIndex;
}


//[-------------------------------------------------------]
//[ Functions                                             ]
//[-------------------------------------------------------]
@insertpiece(DefineGetTangentFrame)


//[-------------------------------------------------------]
//[ Main                                                  ]
//[-------------------------------------------------------]
VS_OUTPUT main(VS_INPUT input)
{
	VS_OUTPUT output;

	// Calculate the world space vertex position
	float4 worldSpacePosition = mul(ObjectSpaceToWorldSpaceMatrix, float4(input.Position * StaticPropertyTest, 1.0f));

	// Calculate the clip space vertex position, left/bottom is (-1,-1) and right/top is (1,1)
	output.Position = mul(WorldSpaceToClipSpaceMatrix, worldSpacePosition);

	// Pass through the decoded 16 bit texture coordinate
	output.TexCoord = input.TexCoord / 32767.0f;

	// Calculate the tangent space to view space tangent, binormal and normal
	// - 16 bit QTangent basing on http://dev.theomader.com/qtangents/ "QTangents" which is basing on
	//   http://www.crytek.com/cryengine/presentations/spherical-skinning-with-dual-quaternions-and-qtangents "Spherical Skinning with Dual-Quaternions and QTangents"
	// TODO(co) float3x3
	float4x4 objectSpaceToViewSpaceMatrix = mul(WorldSpaceToViewSpaceMatrix, ObjectSpaceToWorldSpaceMatrix);
	output.TangentFrame = GetTangentFrame((float3x3)objectSpaceToViewSpaceMatrix, input.QTangent / 32767.0f);

	// Done
	return output;
}
